#Область ПрограммныйИнтерфейс

// Процедура размещения поля ввода КонтактныеЛиыа в формах Документов
//
// Параметры:
//  Форма - Форма - форма документа где должно выводиться поле КонтактноеЛицо
//
Процедура ДополнитьФорму(Форма) Экспорт

ИмяФормы = Форма.ИмяФормы;

Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда 
	ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма);
	ДобавитьГруппуДоработкаСПолемВводаИКомандой(Форма); 
	
ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
	ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма); 
	
ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
	ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма);
	
ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
	ВставитьПолеКонтактноеЛицоПередОснование(Форма); 
	
ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
	ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма);

КонецЕсли

КонецПроцедуры 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппуШапкаПраво(Форма)

	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.svo_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ВставитьПолеКонтактноеЛицоПередСуммаДокумента(Форма)

	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.svo_КонтактноеЛицо";
	
КонецПроцедуры  

Процедура ВставитьПолеКонтактноеЛицоПередОснование(Форма)

	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.svo_КонтактноеЛицо";
	
КонецПроцедуры  

Процедура ДобавитьГруппуДоработкаСПолемВводаИКомандой(Форма)
	
	ГруппаДоработки = Форма.Элементы.Добавить("ГруппаДоработки", Тип("ГруппаФормы"),Форма.Элементы.ГруппаШапкаЛево);
	ГруппаДоработки.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаДоработки.Отображатьзаголовок = Ложь;
	ГруппаДоработки.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ПолеВвода = Форма.Элементы.Добавить("svo_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаДоработки);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.svo_СогласованнаяСкидка";   
	ПолеВвода.УстановитьДействие("ПриИзменении","svo_СогласованнаяСкидкаПриИзменении"); 
	
	НоваяКоманда = Форма.Команды.Добавить("Пересчитать");
	НоваяКоманда.Действие = "svo_ПересчитатьТЧТоваровИУслуг";
		
	Кнопка = Форма.Элементы.Добавить("РасчитатьСкидку", Тип("КнопкаФормы"),Форма.Элементы.ГруппаДоработки);
	Кнопка.Картинка = БиблиотекаКартинок.Обновить;
	Кнопка.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	Кнопка.Отображение = ОтображениеКнопки.КартинкаИТекст;
	Кнопка.Заголовок = "Пересчитать";
	Кнопка.ИмяКоманды = "Пересчитать";
	
	
КонецПроцедуры	    

#КонецОбласти



